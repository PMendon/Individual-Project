/*NOTE ON NOMENCLATURE: Each dataset has the base name ashline. Any change to the dataset will result in the creation of a new dataset called ashlineX where X is 
the next sequential integer (ashline2, ashline3, ashline4, ...). The final iteration of the dataset for each analysis will be called ashlineY where Y is the type of 
analysis (Primary, Sensitivity, and mi)*/ 

/*Convert raw data to csv for platform independent analysis*/ 
LIBNAME ashline 'C:\Users\pmendon\Downloads'; 

DATA ashline; 
	set ashline.al_mental_hlth; 
RUN; 

PROC EXPORT data=ashline outfile= 'C:\Users\pmendon\Downloads\ashline.csv' dbms=csv replace;
RUN; 

/*Determine variables in dataset*/ 
PROC IMPORT DATAFILE='C:\Users\pmendon\Downloads\ashline.csv' DBMS=CSV OUT=ashline;
	GETNAMES=YES;
RUN;

PROC MEANS data=ashline; 
RUN; 

/*Determine demographic data*/ 
PROC SORT data=ashline; 
	by quit30; 
RUN; 

PROC FREQ data=ashline;  
	tables quit30*race;
RUN; 

PROC MEANS data=ashline;  
	var clientage;
RUN; 

PROC FREQ data=ashline; 
	tables quit30*gender; 
RUN; 

/*Determine descriptive statistics*/ 
PROC MEANS data=ashline; 
	var fagerstrom clientage; 
RUN; 

PROC FREQ data=ashline; 
	tables quit30*gender; 
	tables quit30*referral; 
	tables quit30*q10; 
	tables quit30*coaching_calls_beforeFU; 
	tables quit30*medication; 
RUN; 

/*Convert age=0 observations to age='.', 0 is not a valid age*/ 
DATA ashline2; 
	set ashline; 
		if clientage=0 then clientage='.'; 
RUN; 

/*Assess missing data in outcome and predictors*/ 
PROC MEANS data=ashline2 n nmiss; 
	var quit30 gender clientage fagerstrom referral medication coaching_calls_beforeFU q10 asthma hypertension cancer copd diabetes heartdisease; 
RUN; 

/*Combine the different comorbid conditions into one variable count*/ 
DATA ashline3;
	set ashline2; 
		CC=0; 
			ARRAY disease {6} asthma hypertension cancer copd diabetes heartdisease; 
			DO i=1 to 6; 
				if disease{i} = 1 THEN CC=CC+1; 
			END; 
	drop i; 
RUN;  

/*Combine NRT and Coaching Calls into one variable*/ 
DATA ashline4;
	set ashline3;
		NumInterventions='.';
		if medication=0 and coaching_Calls_beforeFU =0 then numinterventions =0;
		if medication=1 and coaching_Calls_beforeFU =0 then numinterventions =1;

		if medication=0 and coaching_calls_beforeFU =>1 and coaching_calls_beforeFU <=4 then numinterventions=2;
		if medication=1 and coaching_calls_beforeFU =>1 and coaching_calls_beforeFU <=4 then numinterventions=3;
	
		if medication=0 and coaching_calls_beforeFU >=5 then numinterventions=4;
		if medication=1 and coaching_calls_beforeFU >=5 then numinterventions=5;

		if medication='.' and coaching_calls_beforeFU =0 then numinterventions=6; 
		if medication='.' and coaching_calls_beforeFU >=1 and coaching_calls_beforeFU <=5 then numinterventions=7;
		if medication='.' and coaching_calls_beforefu >=5 then numinterventions=8;
RUN;

/*Turn q10 (Do others smoke at home?) into a binary variable*/ 
DATA ashline5;
	set ashline4;
		household=.;
		if q10= 0 then household=0;
		if q10= 1 then household=1;
		if q10= 99 then household=1;
RUN;

/*Drop missing outcome variable patients*/ 
DATA ashline6; 
	set ashline5;
		if quit30='.' then delete; 
RUN; 

/*Drop ecig users*/ 
DATA ashlinePrimary; 
	set ashline6; 
		if ecig = 1 then delete;
RUN; 

/*Perform initial analysis, Logistic Regression*/ 
/*Note: quit30 is "did you use tobacco in the last 30 days?" Y is a 1, N is a 0. N is the outcome "quit smoking" so 1 is our reference group*/ 
PROC LOGISTIC data=ashlinePrimary plots(only)=roc;
		class gender household referral numinterventions / param=ref ref=first; 
		model quit30 (event='0') = gender referral numinterventions cc clientage fagerstrom household; 
		ods select globaltests oddsratios roccurve;
RUN;

/*Sensitivity Analysis 1: Create an interaction term for Fagerstrom and Household Smokers, see Chandola, T., Head, J., & Bartley, M. (2004).*/ 
DATA ashlineSensitivity; 
	set ashlinePrimary; 
		interact=fagerstrom*household; 
RUN; 

/*Perform Logistic Regression, include interaction term*/ 
PROC LOGISTIC data=ashlineSensitivity plots(only)=roc;
		class gender household referral numinterventions / param=ref ref=first; 
		model quit30 (event='0') = gender referral numinterventions cc clientage fagerstrom household interact; 
		ods select globaltests oddsratios roccurve;
RUN;

/*Sensitivity Analysis 2: Add presence of mental health treatments into the model*/ 
PROC LOGISTIC data=ashlineSensitivity plots(only)=roc;
		class gender household referral numinterventions / param=ref ref=first; 
		model quit30 (event='0') = gender referral numinterventions cc clientage fagerstrom household mental_treat; 
		ods select globaltests oddsratios roccurve;
RUN;

/*Sensitivity Analysis 3: Impute Missng Data using Multiple Imputation by Chained Equations (MICE)*/ 
/*At this point, we exported dataset from SAS to STATA and performed the imputation in STATA since it failed to converge in SAS*/
PROC EXPORT data=ashline5 outfile= 'C:\Users\pmendon\Downloads\ashline.csv' dbms=csv replace;
RUN; 


